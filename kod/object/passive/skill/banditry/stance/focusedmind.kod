% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
FocusedMind is Stance

constants:

   include blakston.khd
   include protocol.khd   

resources:

   FocusedMind_name_rsc = "focused mind"
   FocusedMind_icon_rsc = isecwind.bgf
   FocusedMind_desc_rsc = "A master stance that expends spiritual energy to resist damage."

   FocusedMind_skill_intro = "Banditry Lv. 6: A stance that protects the warrior by calling upon his spiritual energy."
   
   FocusedMind_begin = "The world sharpens and comes into focus as you let mana flow through you."
   FocusedMind_end = "You relax your thoughts."

classvars:

   vrName = FocusedMind_name_rsc
   vrIcon = FocusedMind_icon_rsc
   vrDesc = FocusedMind_desc_rsc
   
   stance_begin_msg = FocusedMind_begin
   stance_end_msg = FocusedMind_end   

   vrSkill_intro = FocusedMind_skill_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_FOCUSED_MIND
   viSchool = SKS_BANDITRY
   viSkill_Level = 6
   viChance_to_Increase = 5
   viMeditate_ratio = 500

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetIntellect);
   }

   LastPriorityModifyDefenseDamage(who=$,what=$,damage=$,atype=$,aspell=$)
   {
      local iFocusedMindAbility, iStat, iDamageReduction, iFinalDamage, iLoseMana;

      iFocusedMindAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);
      
      % Up to 5 damage reduction from skill, up to 1-2 from stat
      iDamageReduction = ((iFocusedMindAbility+1) / 20) + (iStat / 35);
      iDamageReduction = Bound(iDamageReduction,0,5);

      iFinalDamage = damage - iDamageReduction;
      iLoseMana = (damage-iFinalDamage) * 2;
      
      if iFinalDamage < damage
         AND Send(who,@GetMana) >= iLoseMana
      {
         Send(self,@ImproveAbility,#who=who,#target=what);  
         Send(who,@LoseMana,#amount=iLoseMana);
         return iFinalDamage;
      }
      
      return damage;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
