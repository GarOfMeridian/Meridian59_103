% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Rescue is Spell

constants:

   include blakston.khd

resources:

   rescue_name_rsc = "rescue"
   rescue_icon_rsc = rescuico.bgf
   rescue_desc_rsc = \
      "Calls upon Shal'ille to transport you back to "
      "safety.  Ability determines how fast you are rescued.  "
      "Requires emerald to cast."

   Rescue_Start = "You start your rescue preparations."
   Rescue_Success = "You feel a holy force rescue you from your current situation."
   Rescue_Already_Cast = "You are already being rescued."
   Rescue_Token_Cancel = "You cannot rescue while holding a token."
   
   Rescue_depart_to_destination = "%s vanishes in a burst of protective light!"
   
   rescue_sound = srescue.wav

classvars:

   vrName = rescue_name_rsc
   vrIcon = rescue_icon_rsc
   vrDesc = rescue_desc_rsc

   Teleport_Start_Rsc = Rescue_Start
   Teleport_Already_Cast_Rsc = Rescue_Already_Cast

   viSpell_num = SID_RESCUE
   viSchool = SS_SHALILLE
   viSpell_level = 3
   viMana = 16

   vrSucceed_wav = rescue_sound
   viChance_To_Increase = 10
   viMeditate_ratio = 30

   viFlash = FLASH_GOOD

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Emerald,1],plReagents);

      return;
   }

   DoTeleport(who = $)
   {
      local i, each_obj, oCurrentRoom, oTargetRoom;

      send(who,@MsgSendUser,#message_rsc=Rescue_Success);
            
      for i in Send(Send(who,@GetOwner),@GetHolderActive)
      {
         each_obj = Send(Send(who,@GetOwner),@HolderExtractObject,#data=i);
         if IsClass(each_obj,&User)
            AND each_obj <> who
         {
            Send(each_obj,@MsgSendUser,#message_rsc=Rescue_depart_to_destination,#parm1=Send(who,@GetName));
         }
      }

      oCurrentRoom = send(who,@GetOwner);

      if oCurrentRoom <> $
      {
         % Teleport to Ko'catan Inn if we're in Kocatan, or at the Pool of Vigor.
         if send(oCurrentRoom,@GetRegion) = RID_KOCATAN
            OR send(oCurrentRoom,@GetRoomNum) = RID_ORC_CAVE5_EXT
         {
            oTargetRoom = send(SYS,@FindRoomByNum,#num=RID_KOC_INN);
            post(oTargetRoom,@Teleport,#what=who);
         
            return;
         }

         % If we're in the orc caves, teleport to the Pool of Vigor.
         % Except: actually being in the Pool of Vigor is handled above.
         if send(oCurrentRoom,@GetRegion) = RID_ORC_CAVE1
         {
            oTargetRoom = send(SYS,@FindRoomByNum,#num=RID_ORC_CAVE5_EXT);
            post(oTargetRoom,@Teleport,#what=who);
         
            return;
         }
      }

      % Default: Go to our home room.
      post(who,@AdminGoToSafety);

      return;
   }

   GetPotionClass()
   {
      return &RescuePotion;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
