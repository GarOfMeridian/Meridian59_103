% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Backstab is BanditrySkill

constants:

   include blakston.khd
   include protocol.khd   

resources:

   Backstab_name_rsc = "backstab"
   Backstab_icon_rsc = isecwind.bgf
   Backstab_desc_rsc = "With the proper skill and timing, any attack can be made lethal."

   Backstab_success = "Seeing a weak point, you strike %s in the back!"

classvars:

   vrName = Backstab_name_rsc
   vrIcon = Backstab_icon_rsc
   vrDesc = Backstab_desc_rsc

   vrSkill_intro = Backstab_skill_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_BACKSTAB
   viSchool = SKS_BANDITRY
   viSkill_Level = 5
   viChance_to_Increase = 10
   viMeditate_ratio = 300

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetAgility);
   }
   
   PriorityModifyDamage(who=$,what=$,damage=$)
   {
      local chance, iAgility, iAbility, iTargetHealth, iTargetMaxHealth, iDamageBonus;

      % This skill can be used by both players and monsters.
      iAgility = Send(who,@GetAgility);
      iAbility = Send(who,@GetSkillAbility,#skill_num=SKID_BACKSTAB);
      
      iTargetHealth = Send(what,@GetHealth);
      if IsClass(what,&Player)
      {
         iTargetMaxHealth = Send(what,@GetMaxHealth);
      }
      if IsClass(what,&Monster)
      {
         iTargetMaxHealth = Send(what,@GetMaxHitPoints);
      }
      
      if iTargetHealth >= iTargetMaxHealth * 9 / 10
      {
         iDamageBonus = 100 + ((iAbility+1) / 5) + (iAgility / 10);
         iDamageBonus = Bound(iDamageBonus,100,127);
         
         return damage * iDamageBonus / 100;
      }
      
      return damage;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
